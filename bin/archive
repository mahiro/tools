#!/usr/bin/perl -w
use strict;
use FindBin;

BEGIN {
	my $local = "$FindBin::RealBin/$FindBin::RealScript.local";
	require $local if -f $local;
}

use Cwd qw(abs_path);
use Date::Parse;
use DateTime;
use DateTime::Format::Duration;
use File::Basename;
use File::Path;
use Getopt::Long;
use IO::Handle;
use POSIX;

sub parse_args {
	my $opt = {};

	GetOptions $opt, qw(
		chdir=s directory=s path=s time
		gmt mtime older=s larger=s
		exec=s
		force interactive verbose
		show help
	) or die $!;

	if ($opt->{help}) {
		my $cmd = basename($0);

		print <<END;
Usage: $cmd [OPTIONS] [FILES..]

Move the specified files (or files in the current directory if omitted) to an
archive directory, classifying the files by the date/time.

Options:
  -c WORKING_DIR, -chdir WORKING_DIR
    Chdir (cd) to the WORKING_DIR before everything else.

  -d ARCHIVE_DIR, -directory ARCHIVE_DIR
    Specify archive directory. Default = ./archives

  -p PATTERN, -path PATTERN
    Specify date/time format (strftime). Default = \%Y/\%m/\%d
    PATTERN can contain variables as below (\$varname or \${varname}):
      \$name: the base name including the file extension
      \$base: the base name excluding the file extension
      \$ext: the file extension including the dot ('.') if any
      \$extname: the file extension excluding the dot ('.') if any
      \$timestamp: unix time stamp in seconds
      \$long: short for '%Y/%m/%d/\$name.%Y%m%d-%H%M%S'
    If PATTERN does not contain any variables or ends with a slash ('/'),
    it is regarded as a directory where the file will be moved.

  -t, -time
    Same as -path '\$long' unless any -path option is specified.

  -g, -gmt
    Use GMT intead of local time.

  -m, -mtime
    Always use each file's mtime instead of trying to interpret the file name.

  -o TIMESPAN, -older TIMESPAN
    Only archive files older than the specified time span (or a specific time).

  -l FILESIZE, -larger FILESIZE
    Only archive files larger than the specified size.

  -e COMMAND, -exec COMMAND
    Run COMMAND after each file is moved. Any occurrences of a pair of braces
    ('{}') will be replaced by the file, or if no {} pairs occur, the file name
    is appended at the end as an argument.
    (E.g. -exec 'gzip -9')

  -f, -force
    Skip the confirmation step at the beginning.

  -i, -interactive
    Confirm each file to move interactively.

  -v, -verbose
    Print each moved file.

  -s, -show
    Print the list of files to archive and exit.

  -h, -help
    Print help and exit.
END

		exit 1;
	}

	$opt->{older} = parse_timespan($opt->{older}) if defined $opt->{older};
	$opt->{larger} = parse_filesize($opt->{larger}) if defined $opt->{larger};
	$opt->{path} = '$long' if $opt->{time} && !defined $opt->{path};
	return wantarray ? ($opt, @ARGV) : $opt;
}

sub parse_timespan {
	my ($input) = @_;

	if ($input =~ /^\s*(\d+)\s*$/) {
		die "time unit must be provided: $input";
	} elsif ($input =~ /^\s*(\d+|\d*\.\d+)\s*(\w+)\s*$/) {
		my ($num, $unit) = ($1, lc($2));
		my $d;

		if ($unit =~ /^y((ea)?r)?s?$/) {
			$d = DateTime::Duration->new(years => $num);
		} elsif ($unit =~ /^mo(n(th)?)?s?$/) {
			$d = DateTime::Duration->new(months => $num);
		} elsif ($unit =~ /^w(ee)?k?s?$/) {
			$d = DateTime::Duration->new(weeks => $num);
		} elsif ($unit =~ /^da?y?s?$/) {
			$d = DateTime::Duration->new(days => $num);
		} elsif ($unit =~ /^h(ou)?r?s?$/) {
			$d = DateTime::Duration->new(hours => $num);
		} elsif ($unit =~ /^m(i(n(ute)?)?)?s?$/) {
			$d = DateTime::Duration->new(minutes => $num);
		} elsif ($unit =~ /^s(e(c(ond)?)?)?s?$/) {
			$d = DateTime::Duration->new(seconds => $num);
		} else {
			die "cannot parse time span: $input";
		}

		my $dt = DateTime->now;
		return $dt->subtract_duration($d)->epoch;
	} else {
		return str2time($input);
	}
}

sub parse_filesize {
	my ($input) = @_;

	if ($input =~ /^\s*((?:\d*\.)?\d+)\s*(\w*)\s*$/) {
		my ($num, $unit) = ($1, $2);
		my $d;

		if ($unit eq '' or $unit =~ /^b(ytes?)?$/i) {
			$d = 1;
		} elsif ($unit =~ /^kb?/i) {
			$d = 2 ** 10;
		} elsif ($unit =~ /^mb?/i) {
			$d = 2 ** 20;
		} elsif ($unit =~ /^gb?/i) {
			$d = 2 ** 30;
		} elsif ($unit =~ /^tb?/i) {
			$d = 2 ** 40;
		} else {
			die "unknown unit: $unit";
		}

		return $num * $d;
	} else {
		die "cannot parse file size: $input";
	}
}

sub should_move {
	my ($file, $dir) = @_;
	return 0 if !-e $file || basename($file) =~ /^\.\.?$/;
	return abs_path($file) !~ m{^$dir(/|$)};
}

sub make_time {
	my ($opt, $year, $month, $day, $hour, $minute, $second) = @_;
	
	if ($opt->{gmt}) {
		return DateTime->new(year => $year, month => $month, day => $day,
			hour => $hour, minute => $minute, second => $second, time_zone => 'GMT')->epoch;
	} else {
		return DateTime->new(year => $year, month => $month, day => $day,
			hour => $hour, minute => $minute, second => $second)->epoch;
	}
}

sub get_time {
	my ($opt, $file) = @_;

	unless ($opt->{mtime}) {
		my $base = basename($file);

		if ($base =~ /\b(\d{4})(\d{2})(\d{2})[\-\._]?(\d{2})(\d{2})(\d{2})\b/) {
			return make_time($opt, $1, $2, $3, $4, $5, $6);
		} elsif ($base =~ /\b(\d{4})[\-\._](\d{2})[\-\._](\d{2})[\-\._](\d{2})[\-\._](\d{2})[\-\._](\d{2})\b/) {
			return make_time($opt, $1, $2, $3, $4, $5, $6);
		} elsif ($base =~ /\b(\d{4})(\d{2})(\d{2})\b/) {
			return make_time($opt, $1, $2, $3, 0, 0, 0);
		} elsif ($base =~ /\b(\d{4})[\-\._](\d{2})[\-\._](\d{2})\b/) {
			return make_time($opt, $1, $2, $3, 0, 0, 0);
		}
	}

	return ((stat $file)[9]);
}

sub apply_variables {
	my ($text, $map) = @_;

	my $conv = sub {
		my ($orig) = @_;
		my $name = $orig;
		$name =~ s/^\$\{?|\}$//g;
		return defined $map->{$name} ? $map->{$name} : $orig;
	};

	$text =~ s/(\$[A-Za-z]+|\$\{[A-Za-z]+\})/&$conv($1)/eg;
	return $text;
}

sub expand_common_variables {
	my ($path) = @_;

	my $map = {
		long => '%Y/%m/%d/$name.%Y%m%d-%H%M%S',
	};

	return apply_variables($path, $map);
}

sub expand_file_variables {
	my ($opt, $file, $time, $path) = @_;

	$path = ($opt->{gmt} ?
			strftime($path, gmtime $time) :
			strftime($path, localtime $time));

	if ($path =~ /\$/) {
		my $name = basename($file);
		my $base = $name;
		my $ext = '';

		if ($name =~ /^(.+)(\.[^\.]+)$/) {
			($base, $ext) = ($1, $2);
		}

		(my $extname = $ext) =~ s/^\.//;

		my $map = {
			name => $name,
			base => $base,
			ext => $ext,
			extname => $extname,
			timestamp => $time,
		};

		$path = apply_variables($path, $map);
		return $path =~ m{/$} ? $path.basename($file) : $path;
	} else {
		return "$path/".basename($file);
	}
}

sub move_file {
	my ($opt, $file, $time, $dir, $absdir, $path) = @_;
	$path = expand_file_variables($opt, $file, $time, $path);

	my $dst_file = "$absdir/$path";
	my $dst_dir = dirname($dst_file);

	if (-e $dst_file) {
		if ($opt->{interactive}) {
			print "$dst_file already exists. Overwrite? [y/N] ";
			STDOUT->flush;
			return if <STDIN> !~ /^y/i;
		} else {
			print STDERR "$dst_file: already exists\n";
			return;
		}
	}

	if ($opt->{interactive}) {
		print dirname($path), ": ", basename($path), ": Archive? [y/N] ";
		STDOUT->flush;
		return if <STDIN> !~ /^y/i;
	}

	mkpath $dst_dir if !-d $dst_dir;

	print "mv $file $dst_file\n" if $opt->{verbose};
	rename $file, $dst_file;

	if (defined $opt->{exec}) {
		my $cmd = $opt->{exec};
		my $param = $dst_file;
		$param =~ s/'/\\'/g;
		$cmd .= " '$param'" if $cmd !~ /\{\}/;
		$cmd =~ s/\{\}/'$param'/g;

		print "$cmd\n" if $opt->{verbose};
		system $cmd;
	}
}

sub show_files {
	my ($opt, $plan, $path) = @_;

	for my $file (sort {$plan->{$a} <=> $plan->{$b}} keys %$plan) {
		my $time = $plan->{$file};
		my $dst = expand_file_variables($opt, $file, $time, $path);
		print dirname($dst), ": ", basename($dst), "\n";
	}
}

sub main {
	my ($opt, @files) = parse_args();

	# Chdir
	if (defined $opt->{chdir}) {
		chdir $opt->{chdir} or die "$opt->{chdir}: $!";
	}

	# Configure input
	my $dir = defined $opt->{directory} ? $opt->{directory} : 'archives';
	my $absdir = abs_path($dir);

	my $path = defined $opt->{path} ? $opt->{path} : '%Y/%m/%d';
	$path = expand_common_variables($path);

	@files = <./*> if @files == 0; # do not include '.*' unless dotglob is on

	# Make plan
	my $plan = {};

	for my $file (@files) {
		if (should_move($file, $absdir)) {
			my $time = get_time($opt, $file);

			if (!defined $opt->{older} or $time < $opt->{older}) {
				if (!defined $opt->{larger} or -s $file > $opt->{larger}) {
					$plan->{$file} = $time;
				}
			}
		}
	}

	if ($opt->{show}) {
		show_files($opt, $plan, $path);
		exit;
	}

	# Confirm
	my $num_files = scalar(keys %$plan);

	if ($num_files == 0) {
		print "No files to move\n" unless $opt->{force};
		exit 0;
	}

	unless ($opt->{force}) {
		print "Number of files to move: $num_files\n";
		print "Destination: $dir/$path\n";
		print "(Directory will be created)\n" unless -d $dir;

		if (defined $opt->{older}) {
			my $older_fmt = strftime('%F %T', $opt->{gmt} ?
					gmtime($opt->{older}) :
					localtime($opt->{older}));

			print "Older than: $older_fmt\n";
		}

		print "\n";
		print "Proceed? [yes/*no/show] ";
		STDOUT->flush;
		my $answer = <STDIN>;
		my $proceed = 0;

		if ($answer =~ /^y/i) {
			$proceed = 1;
		} elsif ($answer =~ /^s/i) {
			print "\n";
			show_files($opt, $plan, $path);
			print "\n";

			print "Proceed? [yes/*no] ";
			STDOUT->flush;
			$answer = <STDIN>;
			$proceed = ($answer =~ /^y/i);
		}

		unless ($proceed) {
			print "Canceled\n";
			exit 1;
		}
	}

	# Execute
	mkpath $dir if !-d $dir;

	for my $file (@files) {
		if (exists $plan->{$file}) {
			move_file($opt, $file, $plan->{$file}, $dir, $absdir, $path);
		}
	}
}

main();
